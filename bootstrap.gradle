buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5' }
}

repositories { jcenter() }

apply plugin: 'groovy'
apply plugin: 'idea'

group = 'com.netflix.nebula'

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

apply plugin: 'bintray'
def packageName = name
bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'nebula'
        name = packageName
        licenses = ['Apache-2.0']
    }
    // Will have to manually publish on bintray.com
}
bintrayUpload.dependsOn(build)

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installed $project.name"
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.netflix.nebula:nebula-project-plugin:1.12.0-SNAPSHOT'
    compile('com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3') {
        exclude group: 'org.codehaus.groovy.modules.http-builder', module: 'http-builder'
    }
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
        exclude group: 'net.sourceforge.nekohtml', module: 'nekohtml'
    }
    compile('org.jfrog.buildinfo:build-info-extractor-gradle:2.2.4') // Way too many dependencies. :-(
    compile 'com.github.townsfolk:gradle-release:1.2'

    // will get nebula-test from ourselves, the plugin, but we should control the version here
    testCompile 'com.netflix.nebula:nebula-test:1.12.0-SNAPSHOT'
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
