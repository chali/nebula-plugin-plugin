buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'nebula.maven-publish' version '4.4.4'
    id 'nebula.maven-apache-license' version '4.4.4'
    id 'nebula.javadoc-jar' version '4.4.4'
    id 'nebula.source-jar' version '4.4.4'
    id 'nebula.info' version '3.0.3'
    id 'nebula.contacts' version '3.0.1'
    id 'nebula.nebula-release' version '3.1.1'
    id 'nebula.nebula-bintray' version '3.1.0'
    id 'nebula.dependency-lock' version '4.2.0'
    id 'nebula.integtest' version '3.0.3'
    id 'com.gradle.plugin-publish' version '0.9.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'
}

description 'Gradle plugin to setup nebula-plugin'

group = 'com.netflix.nebula'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()

    compile 'com.netflix.nebula:gradle-info-plugin:latest.release'
    compile 'com.netflix.nebula:gradle-contacts-plugin:latest.release'
    compile 'com.netflix.nebula:nebula-publishing-plugin:latest.release'
    compile 'com.netflix.nebula:nebula-release-plugin:latest.release'
    compile 'com.netflix.nebula:nebula-bintray-plugin:latest.release'
    compile 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:latest.release'

    testCompile 'org.ajoberstar:grgit'
    testCompile('com.netflix.nebula:nebula-test:latest.release') {
        exclude group: 'org.codehaus.groovy'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-plugin-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-plugin-plugin.git'

    plugins {
        pluginplugin {
            id = 'nebula.plugin-plugin'
            displayName = 'Nebula Plugin Plugin'
            description = 'Sets up publishing and release process for all of the other nebula plugins'
            tags = ['nebula', 'plugin']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-plugin-plugin'
    }
}

// Wire up build and publishing process

tasks.matching { it.name == 'bintrayUpload' || it.name == 'artifactoryPublish'}.all { Task task ->
    task.mustRunAfter('build')
    tasks.release.dependsOn(task)
}

tasks.matching { it.name == 'bintrayUpload' }.all { Task task ->
    project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        task.onlyIf {
            graph.hasTask(':final') || graph.hasTask(':candidate')
        }
    }
}

project.tasks.matching { it.name == 'artifactoryPublish'}.all { Task task ->
    project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        task.onlyIf {
            graph.hasTask(':snapshot')
        }
    }
}

import org.ajoberstar.gradle.git.release.base.ReleasePluginExtension
ReleasePluginExtension releaseExtension = project.extensions.findByType(ReleasePluginExtension)
releaseExtension.with {
    defaultVersionStrategy = nebula.plugin.release.NetflixOssStrategies.SNAPSHOT
}

if (project.hasProperty('release.travisci') && project.property('release.travisci').toBoolean()) {
    project.tasks.release.deleteAllActions()
    project.tasks.prepare.deleteAllActions()
    nebulaRelease {
        addReleaseBranchPattern(/HEAD/)
        addReleaseBranchPattern(/v?\d+\.\d+\.\d+/)
        addReleaseBranchPattern(/gradle-\d+\.\d+/)
    }
}
